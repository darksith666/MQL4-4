//--------------------------------------------------------------------
// Terminal.mqh
// Предназначен для использования в качестве примера в учебнике MQL4.
#include <Variables.mqh>
//--------------------------------------------------------------- 1 --
// Функция учёта ордеров
// Глобальные переменные:
// Mas_Ord_New[31][9]   // Массив ордеров последний известный
// Mas_Ord_Old[31][9]   // Массив ордеров предыдущий (старый)
                        // 1й индекс = порядковый номер ордера 
                        // [][0] не определяется
                        // [][1] курс откр. ордера (абс.знач.курса)
                        // [][2] StopLoss ордера   (абс.знач.курса)
                        // [][3] TakeProfit ордера (абс.знач.курса)
                        // [][4] номер ордера        
                        // [][5] колич. лотов орд. (абс.знач.курса)
                        // [][6] тип орд. 0=B,1=S,2=BL,3=SL,4=BS,5=SS
                        // [][7] магическое число ордера
                        // [][8] 0/1 факт наличия комментария
// Mas_Tip[6]           // Массив колич. ордеров всех типов
                        // [] тип орд: 0=B,1=S,2=BL,3=SL,4=BS,5=SS
//--------------------------------------------------------------- 2 --
int Terminal()
  {
   int Qnt=0;                          // Счётчик количества ордеров

//--------------------------------------------------------------- 3 --
   ArrayCopy(Mas_Ord_Old, Mas_Ord_New);// Сохраняем предыдущую историю
   Qnt=0;                              // Обнуление счётчика ордеров
   ArrayInitialize(Mas_Ord_New,0);     // Обнуление массива
   ArrayInitialize(Mas_Tip,    0);     // Обнуление массива
//--------------------------------------------------------------- 4 --
   for(int i=0; i<OrdersTotal(); i++) // По рыночн. и отлож. ордерам
     {
      if((OrderSelect(i,SELECT_BY_POS)==true)     //Если есть следующ.
      && (OrderSymbol()==Symbol()))               //.. и наша вал.пара
        {
         //------------------------------------------------------ 5 --
         Qnt++;                                   // Колич. ордеров
         Mas_Ord_New[Qnt][1]=OrderOpenPrice();    // Курс открытия орд
         Mas_Ord_New[Qnt][2]=OrderStopLoss();     // Курс SL
         Mas_Ord_New[Qnt][3]=OrderTakeProfit();   // Курс ТР
         Mas_Ord_New[Qnt][4]=OrderTicket();       // Номер ордера
         Mas_Ord_New[Qnt][5]=OrderLots();         // Количество лотов
         Mas_Tip[OrderType()]++;                  // Кол. ордеров типа
         Mas_Ord_New[Qnt][6]=OrderType();         // Тип ордера
         Mas_Ord_New[Qnt][7]=OrderMagicNumber();  // Магическое число 
         if (OrderComment()=="")
            Mas_Ord_New[Qnt][8]=0;                // Если нет коммент
         else
            Mas_Ord_New[Qnt][8]=1;                // Если есть коммент
         //------------------------------------------------------ 6 --
        }
     }
   Mas_Ord_New[0][0]=Qnt;                         // Колич. ордеров
//--------------------------------------------------------------- 7 --
   return(0);
  }
//--------------------------------------------------------------- 8 --
